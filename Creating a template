pipeline {
    agent any
    
    parameters {
        string(name: 'TEMPLATE_NAME', defaultValue: '', description: 'Name for the template')
        string(name: 'TAG', defaultValue: '', description: 'Tag for the template')
        string(name: 'VALUE', defaultValue: '', description: 'Value for the tag')
    }

    environment {
        ZABBIX_API_URL = "http://0.0.0.0/api_jsonrpc.php"
    }

    stages {
        stage('Authenticate to Zabbix API') {
            steps {
                script {
                    withCredentials([usernamePassword(credentialsId: 'ZABBIX_USER', usernameVariable: 'ZABBIX_USERNAME', passwordVariable: 'ZABBIX_PASSWORD')]) {
                        def authPayload = [
                            jsonrpc: '2.0',
                            method: 'user.login',
                            params: [
                                user: env.ZABBIX_USERNAME,
                                password: env.ZABBIX_PASSWORD
                            ],
                            id: 1,
                            auth: null
                        ]

                        def response = httpRequest(
                            contentType: 'APPLICATION_JSON',
                            httpMode: 'POST',
                            requestBody: groovy.json.JsonOutput.toJson(authPayload),
                            url: "${env.ZABBIX_API_URL}"
                        )

                        if (response.status == 200) {
                            def authResult = readJSON(text: response.content)
                            if (authResult.result) {
                                def authToken = authResult.result
                                createTemplate(authToken)
                            } else {
                                error "Authentication failed: ${authResult.error.message}"
                            }
                        } else {
                            error "Failed to connect to Zabbix API: ${response.status}"
                        }
                    }
                }
            }
        }
    }
}

def createTemplate(authToken) {
    def templatePayload = [
        jsonrpc: '2.0',
        method: 'template.create',
        params: [
            host: params.TEMPLATE_NAME,
            groups: [
                groupid: 1
            ],
            tags: [
                [
                    tag: params.TAG,
                    value: params.VALUE
                ]
            ]
        ],
        auth: authToken,
        id: 1
    ]

    def response = httpRequest(
        contentType: 'APPLICATION_JSON',
        httpMode: 'POST',
        requestBody: groovy.json.JsonOutput.toJson(templatePayload),
        url: "${env.ZABBIX_API_URL}"
    )

    if (response.status == 200) {
        def templateResult = readJSON(text: response.content)
        if (templateResult.result) {
            println "Template created successfully"
        } else {
            error "Failed to create template: ${templateResult.error.message}"
        }
    } else {
        error "Failed to connect to Zabbix API: ${response.status}"
    }
}


